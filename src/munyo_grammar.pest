char = _{
    !("\\" | "|" | "\t" | "\r" | "\n") ~ ANY
}

comment_char = _{
    !("\r" | "\n") ~ ANY
}

char_seq = @{
    char+
}

escaped = {
    "\\\\"
  | "\\|"
  | "\\n"
  | "\\t"
  | "\\r"
}

content = {
    (char_seq | escaped)+
}

tabs = {
    "\t"*
}

new_line = { "\n" | "\r\n" | "\r" | EOI }

param_item = { "|" ~ content }

main_line = {
    tabs ~ line_start_symbol? ~ content ~ param_item* ~ line_end
}

continued_line = {
    tabs ~ content ~ param_item* ~ line_end
}

continuation = {
    tabs ~ param_item+ ~ line_end
}

line_end = {
    normal_end
  | backslash_end
  | single_bar
  | triple_bars
  | double_bars
}

triple_bars   = { "|||" ~ comment_char* ~ new_line ~ continued_line }
double_bars   = { "||" ~ comment_char* ~ new_line ~ continuation? }
single_bar    = { "|" ~ new_line ~ continued_line }
normal_end    = { new_line ~ continuation? }
backslash_end = { "\\" ~ new_line ~ continued_line }

line_start_symbol = {
    ">>>"
  | ">>"
  | ">\\"
  | ">"
  | "\\>>>"
  | "\\>>"
  | "\\>"
}

empty_line = {
    tabs ~ new_line
}

commented_line = {
    tabs ~ "||" ~ comment_char* ~ new_line
}

file = {
    SOI ~ (tabs ~ (main_line | empty_line | commented_line))*
}
